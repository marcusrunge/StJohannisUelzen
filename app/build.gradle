import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
    id 'com.google.gms.google-services'
}
def secretsProperties = new Properties()
def secretsFile = rootProject.file("secrets.properties")
if (secretsFile.exists()) {
    secretsFile.withInputStream { secretsProperties.load(it) }
}
android {
    defaultConfig {
        applicationId "com.marcusrunge.stjohannisuelzen"
        versionCode 398
        versionName '1.4.4'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        minSdk 30
        compileSdk 36
        targetSdk 36
    }

    applicationVariants.configureEach { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }

    signingConfigs {
        release {
            if (secretsProperties.getProperty('keystore.path') != null) {
                storeFile file(secretsProperties.getProperty('keystore.path'))
                storePassword secretsProperties.getProperty('keystore.password')
                keyAlias secretsProperties.getProperty('keystore.alias')
                keyPassword secretsProperties.getProperty('key.password')
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    dataBinding {
        enabled = true
    }
    tasks.withType(KotlinJvmCompile).configureEach {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_1_8)
            freeCompilerArgs.add("-opt-in=kotlin.RequiresOptIn")
        }
    }
    secrets {
        // Optionally specify a different file name containing your secrets.
        // The plugin defaults to "local.properties"
        propertiesFileName = "secrets.properties"

        // A properties file containing default secret values. This file can be
        // checked in version control.
        defaultPropertiesFileName = "local.defaults.properties"

        // Configure which keys should be ignored by the plugin by providing regular expressions.
        // "sdk.dir" is ignored by default.
        ignoreList.add("keyToIgnore") // Ignore the key "keyToIgnore"
        ignoreList.add("sdk.*")       // Ignore all keys matching the regexp "sdk.*"
    }
    namespace 'com.marcusrunge.stjohannisuelzen'
}

dependencies {
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.10.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.10.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation 'androidx.navigation:navigation-fragment-ktx:2.9.6'
    implementation 'androidx.navigation:navigation-ui-ktx:2.9.6'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.3.0'
    implementation 'androidx.webkit:webkit:1.14.0'
    implementation 'androidx.work:work-runtime-ktx:2.11.0'
    implementation project(path: ':core')
    implementation project(path: ':apiconnect')
    implementation project(path: ':dailymotto')
    implementation project(path: ':notification')
    implementation project(path: ':newsfeed')
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation 'com.google.android.gms:play-services-maps:19.2.0'
    implementation 'com.google.android.gms:play-services-location:21.3.0'
    implementation 'com.google.firebase:firebase-config:23.0.1'
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
}
